@enlighten_soc_cost = 5
@enlighten_nrg_cost = 10
technological_enlightenment_6 = {
    icon = "GFX_fleet_task_technological_enlightenment_action"
    small_icon = 3
    resources = {
        category = station_observer_missions
        produces = {
            trigger = {
                exists = owner
                owner = {
                    is_galactic_community_member = yes
                }
                OR = {
                    is_active_resolution = "resolution_galacticcooperation_indigenous_protection_protocols"
                    is_active_resolution = "resolution_galacticcooperation_interstellar_mediation"
                    is_active_resolution = "resolution_galacticcooperation_sanctity_of_life"
                }
            }
            unity = 6
        }
        upkeep = {
            society_research = @enlighten_soc_cost
            energy = @enlighten_nrg_cost
        }
    }
    monthly_progress = 0.015
    completion_event = observation.2000
    potential = {
        FROM = {
            exists = owner
            owner = {
                has_country_flag = industrial_age
            }
            owner = {
                NOT = {
                    has_ethic = ethic_fanatic_xenophobe
                }
            }
        }
        has_policy_flag = enlightenment_allowed
        NOR = {
            has_valid_civic = civic_fanatic_purifiers
            has_valid_civic = civic_hive_devouring_swarm
            has_valid_civic = civic_machine_terminator
        }
    }
    valid = {
        has_policy_flag = enlightenment_allowed
        custom_tooltip = {
            fail_text = CANNOT_ENLIGHTEN_FANATIC_XENOPHOBE
            FROM = {
                exists = owner
                owner = {
                    NOT = {
                        has_ethic = ethic_fanatic_xenophobe
                    }
                }
            }
        }
        NOR = {
            has_valid_civic = civic_fanatic_purifiers
            has_valid_civic = civic_hive_devouring_swarm
            has_valid_civic = civic_machine_terminator
        }
    }
    ai_weight = {
        weight = 10
        modifier = {
            factor = 0
            NOT = {
                has_ai_personality_behaviour = uplifter
            }
        }
        modifier = {
            factor = 0
            resource_stockpile_compare = {
                resource = energy
                value < 1500
            }
        }
    }
}