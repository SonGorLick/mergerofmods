@enlighten_soc_cost = 5
@enlighten_nrg_cost = 10
vbp_hive_break_apart_1 = {
    icon = "GFX_fleet_task_covert_infiltration_action"
    small_icon = 4
    resources = {
        category = station_observer_missions
        upkeep = {
            society_research = @enlighten_soc_cost
            energy = @enlighten_nrg_cost
        }
        produces = {
            trigger = {
                exists = owner
                owner = {
                    is_galactic_community_member = yes
                }
                OR = {
                    is_active_resolution = "resolution_commerce_underdeveloped_system_utilization"
                    is_active_resolution = "resolution_commerce_holistic_asset_coordination"
                    is_active_resolution = "resolution_commerce_profit_maximization_engines"
                }
            }
            minerals = 8
        }
    }
    monthly_progress = 0.02
    completion_event = vbp_hive.430
    potential = {
        NOT = {
            has_valid_civic = civic_inwards_perfection
        }
        NOT = {
            has_authority = auth_hive_mind
        }
        has_ascension_perk = ap_evolutionary_mastery
        FROM = {
            exists = owner
            owner = {
                is_country_type = vbp_hive
            }
        }
    }
    in_breach_of = {
         {
            key = resolution_galacticcooperation_indigenous_protection_protocols
        }
         {
            key = resolution_galacticcooperation_interstellar_mediation
        }
         {
            key = resolution_galacticcooperation_sanctity_of_life
        }
    }
    valid = {
        has_technology = tech_gene_tailoring
        OR = {
            has_policy_flag = interference_active
            has_policy_flag = interference_full
        }
        NOR = {
            has_valid_civic = civic_fanatic_purifiers
            has_valid_civic = civic_hive_devouring_swarm
            has_valid_civic = civic_machine_terminator
        }
    }
    ai_weight = {
        weight = 10
        modifier = {
            factor = 0
            NOT = {
                has_ai_personality_behaviour = infiltrator
            }
        }
        modifier = {
            factor = 0
            resource_stockpile_compare = {
                resource = energy
                value < 1000
            }
        }
        modifier = {
            factor = 0
            OR = {
                is_active_resolution = "resolution_galacticcooperation_indigenous_protection_protocols"
                is_active_resolution = "resolution_galacticcooperation_interstellar_mediation"
                is_active_resolution = "resolution_galacticcooperation_sanctity_of_life"
            }
        }
    }
}