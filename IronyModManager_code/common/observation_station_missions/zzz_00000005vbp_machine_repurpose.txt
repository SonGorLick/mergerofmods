@enlighten_soc_cost = 5
@enlighten_nrg_cost = 10
vbp_machine_repurpose = {
    icon = "GFX_fleet_task_indoctrination"
    small_icon = 4
    resources = {
        category = station_observer_missions
        upkeep = {
            engineering_research = @enlighten_soc_cost
            energy = @enlighten_nrg_cost
        }
        produces = {
            trigger = {
                exists = owner
                owner = {
                    is_galactic_community_member = yes
                }
                OR = {
                    is_active_resolution = "resolution_commerce_underdeveloped_system_utilization"
                    is_active_resolution = "resolution_commerce_holistic_asset_coordination"
                    is_active_resolution = "resolution_commerce_profit_maximization_engines"
                }
            }
            minerals = 8
        }
    }
    monthly_progress = 0.02
    completion_event = vbp_machine.510
    potential = {
        FROM = {
            exists = owner
            owner = {
                is_country_type = vbp_machine
            }
        }
    }
    in_breach_of = {
         {
            key = resolution_galacticcooperation_indigenous_protection_protocols
        }
         {
            key = resolution_galacticcooperation_interstellar_mediation
        }
         {
            key = resolution_galacticcooperation_sanctity_of_life
        }
    }
    valid = {
        OR = {
            has_technology = tech_droid_workers
            has_technology = tech_synthetic_workers
            has_authority = auth_machine_intelligence
        }
        NOR = {
            has_valid_civic = civic_hive_devouring_swarm
            has_policy_flag = robots_outlawed
        }
        custom_tooltip = {
            fail_text = VBP_MACHINE_FINAL_STAGE
            FROM = {
                exists = owner
                owner = {
                    NOT = {
                        has_country_flag = vbp_machine_uplifted
                    }
                }
            }
        }
    }
    ai_weight = {
        weight = 10
        modifier = {
            factor = 0
            resource_stockpile_compare = {
                resource = energy
                value < 1000
            }
        }
        modifier = {
            factor = 2
            OR = {
                has_ai_personality_behaviour = robot_exploiter
                has_ai_personality_behaviour = infiltrator
            }
        }
        modifier = {
            factor = 0
            is_country_type = fallen_empire
        }
        modifier = {
            factor = 0
            OR = {
                is_active_resolution = "resolution_galacticcooperation_indigenous_protection_protocols"
                is_active_resolution = "resolution_galacticcooperation_interstellar_mediation"
                is_active_resolution = "resolution_galacticcooperation_sanctity_of_life"
            }
        }
    }
}
